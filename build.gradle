//     Data Access is a Java library to store data
//     Copyright (C) 2016-2019 Adri√°n Romero Corchado.
//
//     This file is part of Data Access
//
//     Licensed under the Apache License, Version 2.0 (the "License");
//     you may not use this file except in compliance with the License.
//     You may obtain a copy of the License at
//     
//         http://www.apache.org/licenses/LICENSE-2.0
//     
//     Unless required by applicable law or agreed to in writing, software
//     distributed under the License is distributed on an "AS IS" BASIS,
//     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//     See the License for the specific language governing permissions and
//     limitations under the License.

plugins {
  id "com.google.protobuf" version "0.8.10"
  id "java"
  id "maven"
}

group = 'com.github.adrianromero'
version = '1.4.0'

description = """"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/main/grpc'
            srcDirs 'src/generated/main/java'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
    compile group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    compile group: 'com.rabbitmq', name: 'amqp-client', version: '5.7.3'
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.11.0'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.0'
    compile group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '4.2.0'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.9.1'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.23.0'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile group: 'com.auth0', name: 'java-jwt', version: '3.8.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    
    testCompile group: 'org.postgresql', name: 'postgresql', version: '42.2.7'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.199'
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.9.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.23.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

test {
    include 'com/adr/data/test/SuiteAll.class'
    System.properties.each { k, v ->
        if (k.startsWith('app.')) {
            systemProperty k - 'app.', v
        }
    }    
}
