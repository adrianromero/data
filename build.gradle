//     Data Access is a Java library to store data
//     Copyright (C) 2016 Adri√°n Romero Corchado.
//
//     This file is part of Data Access
//
//     Licensed under the Apache License, Version 2.0 (the "License");
//     you may not use this file except in compliance with the License.
//     You may obtain a copy of the License at
//     
//         http://www.apache.org/licenses/LICENSE-2.0
//     
//     Unless required by applicable law or agreed to in writing, software
//     distributed under the License is distributed on an "AS IS" BASIS,
//     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//     See the License for the specific language governing permissions and
//     limitations under the License.

plugins {
    id "org.flywaydb.flyway" version "4.0.1"
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.github.adrianromero'
version = '1.0.22'

description = """"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.rabbitmq', name: 'amqp-client', version: '3.6.2'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.3.1'
    compile group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '3.3.1'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.5'
    compile group: 'org.elasticsearch.client', name: 'transport', version: '5.2.2'
    compile group: 'com.auth0', name: 'java-jwt', version: '3.1.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.zaxxer', name: 'HikariCP', version: '2.5.1'
    
    testCompile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.192'
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '6.0.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

flyway {
    url = System.getProperty('app.database.url')
    user = System.getProperty('app.database.user')
    password = System.getProperty('app.database.password')
    locations = ['filesystem:./src/test/resources/com/adr/data/migration/' + System.getProperty('app.database.migration')]
}

test {
    include 'com/adr/data/test/SuiteAll.class'
    System.properties.each { k, v ->
        if (k.startsWith('app.')) {
            systemProperty k - 'app.', v
        }
    }    
}
